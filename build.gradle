buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'Kihira Repo'
        url = 'http://maven.kihirakreations.co.uk'
    }
}

apply plugin: 'scala'
apply plugin: 'forge'

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

project.ext {
    massive = "0"
    major = "6"
    minor = "0"
    mcVersion = "1.7.10"
    forgeVersion = "10.13.0.1180"
    buildVersion = System.getenv("BUILD_NUMBER")
}

project.version = "${project.ext.mcVersion}-${project.ext.massive}.${project.ext.major}.${project.minor}"
if (project.hasProperty("teamcity") && project.ext.buildVersion != null && teamcity["maven_release"] == null) {
    version = version + ".${System.getenv("BUILD_NUMBER")}"
}

group= "kihira"
archivesBaseName = "FoxLib"

minecraft {
    version = "${project.ext.mcVersion}-${project.ext.forgeVersion}"

    replaceIn "FoxLib.scala"
    replace "@VERSION@", project.version
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.ext.mcVersion
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.api.allSource
    classifier = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.api.output
    classifier = 'dev'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

uploadArchives {
    dependsOn 'reobf'

    if (project.hasProperty("teamcity")) {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: teamcity["maven_url"]) {
                authentication(userName: teamcity["maven_username"], password: teamcity["maven_password"])
            }
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'FoxLib'
                    url 'http://kihirakreations.co.uk'

                    scm {
                        url 'https://github.com/kihira/FoxLib'
                        connection 'scm:git:git://github.com/kihira/FoxLib.git'
                        developerConnection 'scm:git:git@github.com:kihira/FoxLib.git'
                    }

                    issueManagement {
                        system 'github'
                        url 'https://github.com/kihira/FoxLib/issues'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/kihira/FoxLib/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'Kihira'
                            name 'Kihira'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}