buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'Kihira Repo'
        url = 'http://maven.kihirakreations.co.uk'
    }
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

apply plugin: 'scala'
apply plugin: 'forge'
apply plugin: 'maven'

project.ext {
    massive = "0"
    major = "4"
    minor = "1"
    mcVersion = "1.7.10"
    forgeVersion = "10.13.0.1180"
}

version = "${project.ext.mcVersion}-${project.ext.massive}.${project.ext.major}.${project.minor}.${System.getenv("BUILD_NUMBER") ?: 0}"
group= "kihira.foxlib"
archivesBaseName = "FoxLib"

minecraft {
    version = "${project.ext.mcVersion}-${project.ext.forgeVersion}"

    replaceIn "FoxLib.scala"
    replace "@VERSION@", "${version}.${System.getenv("BUILD_NUMBER") ?: 0}"
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.ext.mcVersion
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task userDevJar(type: Jar) {
    from(sourceSets.main.output)
    from(sourceSets.main.allSource)
    classifier = "userdev"
}


artifacts {
    archives userDevJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: project.maven_url) {
            authentication(userName: project.maven_username, password: project.maven_password)
        }
    }
}

task('uploadJars', dependsOn: uploadArchives) {
    description = "Uploads JARs"
}